/*
 * Project: Topp Grasp
 * GitHub: https://github.com/sabroe/Topp-Grasp
 *
 * Copyright 2022-2024 Morten Sabroe Mortensen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * Note:
 *     To publish, run the task 'publish'.
 */

plugins.withType(MavenPublishPlugin) {
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = custom['publishing.publication.groupId']?:project.group
                artifactId = custom['publishing.publication.artifactId']
                version = custom['publishing.publication.groupId']?:project.version
                from components.java
                pom {
                    name = custom['publishing.publication.name']
                    description = custom['publishing.publication.description']
                    url = custom['publishing.publication.url']
                    inceptionYear = custom['publishing.publication.inception-year']

                    licenses {
                        license {
                            name = custom['publishing.publication.licenses.license-1.name']
                            url = custom['publishing.publication.licenses.license-1.url']
                        }
                    }
                    developers {
                        developer {
                            id = custom['publishing.publication.developers.developer-1.id']
                            name = custom['publishing.publication.developers.developer-1.name']
                            email = custom['publishing.publication.developers.developer-1.email']
                        }
                    }
                    scm {
                        connection = custom['publishing.publication.scm.connection']
                        developerConnection = custom['publishing.publication.scm.developer-connection']
                        url = custom['publishing.publication.scm.url']
                    }
                }
            }
        }

        repositories {
            maven {
                /*
                 * Note:
                 *     Setup applicable for the global GitHub located on "github.com".
                 *     To approve the publishing using the Nexus Repository Manager, access --
                 *         https://s01.oss.sonatype.org/#stagingRepositories
                 *     To verify the first stages of approval, access --
                 *         https://repo1.maven.org/maven2/com/yelstream/topp/
                 *         https://repo1.maven.org/maven2/io/github/sabroe/topp/
                 */
                name = 'OSSRH'
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = project.properties['ossrhUsername']
                    password = project.properties['ossrhPassword']
                }
            }
            maven {
                /*
                 * Note:
                 *     Example set for a local, company-specific GitLab instance.
                 *     This shows how to apply different URLs depending upon a special version suffix.
                 */
                name = 'BG'
                def snapshotUrl = 'https://nexus.beumer.com/repository/maven-snapshots/'
                def releaseUrl = 'https://nexus.beumer.com/repository/maven-releases/'
                url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
                credentials {
                    username = project.properties['bgUsername']
                    password = project.properties['bgPassword']
                }
            }
        }
    }
}

plugins.withType(SigningPlugin) {
    signing {
        sign publishing.publications.mavenJava
    }
}
