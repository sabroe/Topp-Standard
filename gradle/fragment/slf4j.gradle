/*
 * Project: Topp Grasp
 * GitHub: https://github.com/sabroe/Topp-Grasp
 *
 * Copyright 2022-2024 Morten Sabroe Mortensen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins.withType(JavaPlugin) {  //Note: 'JavaPlugin' includes 'JavaLibraryPlugin'; both intermediate libraries and end-applications are included!
    def enableFeatureText = custom['feature.slf4j.enable']?.toString()?.trim()
    def enableFeature = enableFeatureText ? enableFeatureText.toBoolean() : true
    if (!enableFeature) {
        logger.debug("Feature 'SLF4J' is disabled.")
    } else {
        dependencies {
            if (dependencies.metaClass.respondsTo(dependencies, 'api')) {
                //Use 'api' if available (implies java-library plugin is applied)
                api 'org.slf4j:slf4j-api:2.0.16'
                api 'org.slf4j:slf4j-ext:2.0.16'
            } else {
                //Fallback to 'implementation' (implies only java plugin is applied)
                implementation 'org.slf4j:slf4j-api:2.0.16'
                implementation 'org.slf4j:slf4j-ext:2.0.16'
            }

            //Apply testImplementation only for intermediate libraries
            if (plugins.hasPlugin('java-library')) {
                testImplementation 'org.slf4j:slf4j-simple:2.0.16'
            }
        }
    }
}
