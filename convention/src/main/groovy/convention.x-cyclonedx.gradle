/*
 * Project: Topp Grasp
 * GitHub: https://github.com/sabroe/Topp-Grasp
 *
 * Copyright 2022-2026 Morten Sabroe Mortensen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * CycloneDX convention plugin.
 *
 * @author Morten Sabroe Mortensen
 * @version 1.0
 * @since 2025-12-17
 */

plugins {
    id 'java' apply false  // Note: Required for type-safe dependency accessors!
    id 'org.cyclonedx.bom' apply false  // See https://plugins.gradle.org/plugin/org.cyclonedx.bom
}

def conventionName = "cyclonedx"

project.logger.info("Convention ${conventionName} loaded.")

project.plugins.withType(JavaPlugin).configureEach {
    def enablePropertyName = "convention.${conventionName}.enable"
    def enable = project.findProperty(enablePropertyName)?.toString()?.trim()?.toBoolean() ?: true
    if (!enable) {
        project.logger.debug("Convention ${conventionName} disabled.")
    } else {
        project.logger.debug("Convention ${conventionName} enabled.")

        project.apply plugin: "org.cyclonedx.bom"

        project.afterEvaluate {
            tasks.named("cyclonedxDirectBom") {
                group = "reporting"
                description = "Generates a per-project CycloneDX SBOM (runtime dependencies only)"

                includeConfigs = ["runtimeClasspath"]
                skipConfigs = ["compileClasspath", "testCompileClasspath"]

                def componentType = org.cyclonedx.model.Component.Type.LIBRARY
                project.plugins.withId("application") {
                    componentType = org.cyclonedx.model.Component.Type.APPLICATION
                }
                project.plugins.withId("java-library") {
                    if (componentType != org.cyclonedx.model.Component.Type.APPLICATION) {
                        componentType = org.cyclonedx.model.Component.Type.LIBRARY
                    }
                }
                projectType = componentType

                def outputDir = project.file("build/reports/cyclonedx-direct")
                jsonOutput.set(new File(outputDir, "bom.json"))
                xmlOutput.set(new File(outputDir, "bom.xml"))
            }
        }
    }
}

/*
 * Note:
 *     To inspect the content of build BOM files, consider this command for ad hoc inspection:
 *     find . -name bom.* | grep -E "(.*)/build/reports/(.*)(bom.xml|bom.json)" | while read r; do echo; echo $r; head -n30 $r; done | less
 */
